cmake_minimum_required(VERSION 3.10)
project(Hint CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

option(VIVADO_BACKEND "Enable Vivado Backend")
option(QUARTUS_BACKEND "Enable Quartus Backend")
option(BITSET_BACKEND "Enable bitset backend" ON)

set(LIBNAME hint)
set(INCLUDE_ROOT ${CMAKE_CURRENT_SOURCE_DIR}/include)

add_library(${LIBNAME} INTERFACE)
target_include_directories(${LIBNAME} INTERFACE ${INCLUDE_ROOT})

if(VIVADO_BACKEND)
	if(NOT DEFINED VIVADO_INCLUDES)
		Message(FATAL_ERROR "Please provide path to Vivado ap_int include in VIVADO_INCLUDES")
	endif()	
	set(VIVADO_INCLUDES ${VIVADO_INCLUDES} CACHE PATH "Path to vivado ap_int includes" FORCE)
	add_library(VIVADO_AP_INT INTERFACE IMPORTED)
	target_include_directories(VIVADO_AP_INT INTERFACE ${VIVADO_INCLUDES})
	target_link_libraries(${LIBNAME} INTERFACE VIVADO_AP_INT)
	target_compile_definitions(${LIBNAME} INTERFACE VIVADO_BACKEND)
endif()
if(QUARTUS_BACKEND)
	if(NOT DEFINED QUARTUS_INCLUDES)
		Message(FATAL_ERROR "Please provide path to quartus ac_int include in QUARTUS_INCLUDES")
	endif()	
	set(QUARTUS_INCLUDES ${QUARTUS_INCLUDES} CACHE PATH "Path to quartus ac_int includes" FORCE)
	add_library(QUARTUS_AP_INT INTERFACE IMPORTED)
	target_include_directories(QUARTUS_AP_INT INTERFACE ${QUARTUS_INCLUDES})
	target_link_libraries(${LIBNAME} INTERFACE QUARTUS_AP_INT)
	target_compile_definitions(${LIBNAME} INTERFACE AC_INT_BACKEND)
endif()

if(BITSET_BACKEND)
	target_compile_definitions(${LIBNAME} INTERFACE BITSET_BACKEND)
endif()

OPTION(BUILD_UNIT_TEST "Build the operators unit tests") 
if(BUILD_UNIT_TEST)
	find_package(Boost 1.55 REQUIRED COMPONENTS unit_test_framework)
	enable_testing()
	add_executable(testVivadoBackend_exe tests/back_end/vivado_main.cpp)
	target_link_libraries(testVivadoBackend_exe ${LIBNAME} ${Boost_LIBRARIES}) 
	add_test(testVivadoBackend testVivadoBackend_exe)
endif()
